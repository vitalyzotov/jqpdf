import org.gradle.internal.jvm.Jvm
import org.gradle.internal.os.OperatingSystem

plugins {
    id 'cpp-library'
    id 'maven-publish'
}

def os = OperatingSystem.current()

group = 'ru.vzotov'
version = "11.4.0.${os.familyName}"

def jvmHome = Jvm.current().javaHome

tasks.withType(CppCompile).configureEach {
    macros.put("POINTERHOLDER_TRANSITION", "4")

    compilerArgs.addAll toolChain.map { toolChain ->
        if (toolChain in VisualCpp) {
            return ['/std:c++17']
        }
        return []
    }
}

library {
    targetMachines = [
            machines.linux.x86_64,
            machines.windows.x86_64
    ]

    linkage = [Linkage.STATIC, Linkage.SHARED]

    binaries.configureEach {
        def compileTask = compileTask.get()

        if (targetMachine.operatingSystemFamily.windows) {
            compileTask.compilerArgs.addAll "-I${jvmHome}/include"
            compileTask.compilerArgs.addAll "-I${jvmHome}/include/win32"
        } else if (targetMachine.operatingSystemFamily.linux) {
            compileTask.compilerArgs.addAll '-I', "${jvmHome}/include"
            compileTask.compilerArgs.addAll '-I', "${jvmHome}/include/linux"
            compileTask.compilerArgs.addAll '-D_FILE_OFFSET_BITS=64'
        } else if (targetMachine.operatingSystemFamily.macOs) {
            compileTask.compilerArgs.addAll '-I', "${jvmHome}/include"
            compileTask.compilerArgs.addAll '-I', "${jvmHome}/include/darwin"
            compileTask.compilerArgs.addAll '-mmacosx-version-min=10.4'
        }
        //compileTask.dependsOn(downloadAndUnzipQPDF)
    }

    dependencies {
        api project(':qpdf')
    }
}

tasks.withType(LinkSharedLibrary).configureEach{
    if (os.linux) {
        linkerArgs.add('-lz')
        linkerArgs.add('-ljpeg')
        linkerArgs.add('-lssl')
        linkerArgs.add('-lcrypto')
        linkerArgs.add('-lgnutls')
    }
}

publishing {
    repositories {
        maven {
            url "../repo"
        }

        maven {
            credentials {
                username "$mavenRepoUser"
                password "$mavenRepoPassword"
            }
            url "https://maven.vzotov.ru/repository/maven-everyone"
        }
    }
}
